D <- readDiscreteCharacterData("data/Primates.nex")

tau <- readTrees("data/primates.tree")[1]

# substition model priors
bf <- v(1,1,1,1)
e <- v(1,1,1,1,1,1)
pi ~ dirichlet(bf)
er ~ dirichlet(e)

# moves on the substitution process parameters
# first some moves only changing one value of the simplex
moves[1] = mvSimplexElementScale(pi, alpha=10.0, tune=true, weight=4.0)
moves[2] = mvSimplexElementScale(er, alpha=10.0, tune=true, weight=6.0)

# the rate matrix
Q := gtr(er,pi)

clockRate <- 1.0

# the sequence evolution model
seq ~ phyloCTMC(tree=tau, Q=Q, branchRates=clockRate, type="DNA")

# attach the data
seq.clamp(D)

mymodel <- model(pi)

monitors[1] <- modelmonitor(filename="performance_primates_GTR.log",printgen=1000, separator = "	")
monitors[2] <- filemonitor(filename="performance_primates_GTR.trees",printgen=1000, separator = "	", tau)
monitors[3] <- screenmonitor(printgen=1000, pi)

 
mymcmc <- mcmc(mymodel, monitors, moves,moveschedule="single")

mymcmc.run(generations=1100000)

q()
